apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:6.0.3'
    }
}

repositories {
    mavenCentral()
}

group = 'net.minecraftforge'
archivesBaseName = 'installer'
version = '2.0'
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

ext {
    MAIN_CLASS = 'net.minecraftforge.installer.SimpleInstaller'
}

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'com.google.guava:guava:14.0'
}

compileJava {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task fatJar(type: Jar, dependsOn: jar) {
    inputs.file jar.archivePath
    from(configurations.compile.collect { zipTree(it).matching { include '**/*.class' } })
    manifest { attributes 'Main-Class': MAIN_CLASS }
}

fatJar.doFirst {
    from zipTree(jar.archivePath)
}

task pgShrinkJar(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    inputs.file fatJar.archivePath
    ext {
        outDir = file("${buildDir}/proguard")
        obfuscatedJar = "${outDir}/${jar.baseName}.jar"
    }
    outDir.mkdirs()

    injars jar.archivePath
    outjars obfuscatedJar
    libraryjars configurations.compile

    // Automatically handle the Java version of this build.
    if (System.getProperty('java.version').startsWith('1.')) {
        // Before Java 9, the runtime classes were packaged in a single jar file.
        libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    } else {
        // As of Java 9, the runtime classes are packaged in modular jmod files.
        libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        libraryjars "${System.getProperty('java.home')}/jmods/java.desktop.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        libraryjars "${System.getProperty('java.home')}/jmods/java.logging.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        libraryjars "${System.getProperty('java.home')}/jmods/jdk.unsupported.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
    }
    configuration 'proguard.pro'
}

task shrinkJar(type: Jar, dependsOn: pgShrinkJar) {
    classifier = 'shrunk'
    from zipTree(pgShrinkJar.obfuscatedJar)
    manifest { attributes 'Main-Class': MAIN_CLASS }
}

artifacts {
    archives fatJar
    archives sourcesJar
    archives shrinkJar
}

jar {
    classifier = 'thin'
    manifest { attributes 'Main-Class': MAIN_CLASS }
}

//TODO: Maven Publish
uploadArchives {
    repositories {
        add getProject().repositories.mavenLocal()
    }
    repositories.mavenDeployer {
	
        if (project.hasProperty('forgeMavenPass')) {
            logger.info('Publishing to files server')
            repository(url: "http://files.minecraftforge.net/maven/manage/upload") {
                authentication(userName: "forge", password: project.getProperty('forgeMavenPass'))
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'Minecraft Forge Installer'
            url 'https://github.com/MinecraftForge/Installer'

            scm {
                url 'https://github.com/MinecraftForge/Installer'
                connection 'scm:git:git://github.com/MinecraftForge/Installer.git'
                developerConnection 'scm:git:git@github.com:MinecraftForge/Installer.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/MinecraftForge/Installer/issues'
            }

            developers {
                developer {
                    id 'cpw'
                    name 'cpw'
                    roles { role 'developer' }
                }
                developer {
                    id 'LexManos'
                    name 'Lex Manos'
                    roles { role 'developer' }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}
