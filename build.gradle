apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:5.2.1'
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net/"
    }
    flatDir {
        name "fileRepo"
        dirs "repo"
    }
}

group = 'net.minecraftforge'
archivesBaseName = 'installer'
version = '1.7'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:4.5'
    compile 'net.sourceforge.argo:argo:3.7'
    compile 'com.google.guava:guava:14.0'
    compile 'org.tukaani:xz:1.3'
    compile 'org.ow2.asm:asm:5.2'
}

task compilej6(type: JavaCompile) {
    source = fileTree(dir: "src/main/java")
    classpath = sourceSets.main.compileClasspath
    destinationDir = sourceSets.main.output.classesDir
    includes = ['net/minecraftforge/installer/Java6Gate.java']
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

compileJava {
    excludes = ['**/Java6Gate.java']

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

tasks.compileJava.dependsOn('compilej6')

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task fatJar(type: Jar, dependsOn: jar) {
    inputs.file jar.archivePath
    from(configurations.compile.collect { zipTree(it).matching { include '**/*.class' } })
    manifest { attributes 'Main-Class': 'net.minecraftforge.installer.Java6Gate' }
}

fatJar.doFirst {
    from zipTree(jar.archivePath)
}

task packerJar(type: Jar, dependsOn: jar) {
    doFirst {
        from zipTree(jar.archivePath)
    }
    classifier = 'packer'
    inputs.file jar.archivePath
    from(configurations.compile.collect { zipTree(it).matching { include '**/*.class' } })
    manifest { attributes 'Main-Class': 'net.minecraftforge.installer.LibraryPacker' }
}

task transformerJar(type: Jar, dependsOn: jar) {
    doFirst {
        from zipTree(jar.archivePath)
    }
    classifier = 'transformer'
    inputs.file jar.archivePath
    from(configurations.compile.collect { zipTree(it).matching { include '**/*.class' } })
    manifest { attributes 'Main-Class': 'net.minecraftforge.installer.transform.LibraryTransformer' }
}


task shrinkJar(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    inputs.file fatJar.archivePath
    ext {
        outDir = file("${buildDir}/proguard")
        obfuscatedJar = "${outDir}/${jar.baseName}.jar"
    }
    outDir.mkdirs()

    injars jar.archivePath
    outjars obfuscatedJar
    libraryjars configurations.compile
    configuration 'proguard.pro'
}

task shrinkOutput(type: Jar, dependsOn: shrinkJar) {
    classifier = 'shrunk'
    from zipTree(shrinkJar.obfuscatedJar)
    manifest { attributes 'Main-Class': 'net.minecraftforge.installer.Java6Gate' }
}

artifacts {
    archives fatJar
    archives sourcesJar
    archives shrinkOutput
    archives packerJar
    archives transformerJar
}

jar {
    classifier = 'thin'
    exclude 'argo/**'
    exclude '**/*.tsrg'
    manifest { attributes 'Main-Class': 'net.minecraftforge.installer.Java6Gate' }
}

uploadArchives {
    repositories {
        add getProject().repositories.mavenLocal()
    }
    repositories.mavenDeployer {
    
        if (project.hasProperty('forgeMavenPass')) {
            logger.info('Publishing to files server')
            repository(url: "http://files.minecraftforge.net/maven/manage/upload") {
                authentication(userName: "forge", password: project.getProperty('forgeMavenPass'))
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'Minecraft Forge Installer'
            url 'https://github.com/MinecraftForge/Installer'

            scm {
                url 'https://github.com/MinecraftForge/Installer'
                connection 'scm:git:git://github.com/MinecraftForge/Installer.git'
                developerConnection 'scm:git:git@github.com:MinecraftForge/Installer.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/MinecraftForge/Installer/issues'
            }

            developers {
                developer {
                    id 'cpw'
                    name 'cpw'
                    roles { role 'developer' }
                }
                developer {
                    id 'LexManos'
                    name 'Lex Manos'
                    roles { role 'developer' }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
